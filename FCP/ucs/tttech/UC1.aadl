package UC1
public
	WITH FORA::platform;
	WITH FORA::hardware;
	WITH FORA::software;
	WITH FORA::core;
	FogNode renames abstract FORA::hardware::FogNode;
	FogNodeSoftwarePlatform renames abstract FORA::platform::FogSoftwarePlatform;
	FogNodeConfiguration renames abstract FORA::software::FogNodeConfiguration;

	abstract FogComputingPlatform
		features
			ClientInterface: requires bus access;
			MaintenanceInterface: requires bus access;
			ApplicationServerInterface: requires bus access;
	end FogComputingPlatform;

	abstract implementation FogComputingPlatform.impl
		subcomponents
			FogNodePrimary: abstract FogNodeInstance.impl;
			FogNodeOptional: abstract FogNodeInstance.impl;
			PLC1: device PLCDevice;
			PLC2: device PLCDevice;
			PLC3: device PLCDevice;
			PLC4: device PLCDevice;
			TSNSwitch1: system FORA::hardware::TSN_Switch;
			TSNSwitch2: system FORA::hardware::TSN_Switch;
			TSNSwitch3: system FORA::hardware::TSN_Switch;
		connections
			PLC1_TSNSwitch1_connection: bus access TSNSwitch1.enet_0 <-> PLC1.TSN_0;
			PLC2_TSNSwitch2_connection: bus access TSNSwitch2.enet_0 <-> PLC2.TSN_0;
			PLC3_TSNSwitch2_connection: bus access TSNSwitch2.enet_0 <-> PLC3.TSN_0;
			PLC4_TSNSwitch3_connection: bus access TSNSwitch3.enet_0 <-> PLC4.TSN_0;
			FogNodePrimary_TSNSwitch2_connection: bus access TSNSwitch2.enet_1 <-> FogNodePrimary.TSN_0;
			FogNodeOptional_TSNSwitch2_connection: bus access TSNSwitch2.enet_1 <-> FogNodeOptional.TSN_0;
	end FogComputingPlatform.impl;

	abstract OPCUADDSGateway
		features
			output: out data port PayloadData;
			input: in data port PayloadData;
	end OPCUADDSGateway;

	abstract implementation OPCUADDSGateway.impl
	end OPCUADDSGateway.impl;

	abstract FogNodeInstance
		features
			TSN_0: requires bus access FORA::hardware::RJ45;
	end FogNodeInstance;

	abstract implementation FogNodeInstance.impl
		prototypes
			SoftwarePlatform: abstract FogNodeSoftwarePlatform;
			HardwarePlatform: abstract FogNode;
		subcomponents
			FogNodeConfiguration: abstract FogNodeConfiguration;
			RTVM: abstract RTVirtualMachine.impl;
			VMs: abstract VirtualMachine[];
			OPCUADDSGatewayVM : abstract OPCUADDSGatewayVM;
			SecuredManagementVM: abstract VirtualMachine;
			FirewallVM: abstract VirtualMachine;
			FogSoftwarePlatform: abstract SoftwarePlatform;
			FogHardwarePlatform: abstract HardwarePlatform;
	end FogNodeInstance.impl;

	abstract OPCUADDSGatewayVM
	end OPCUADDSGatewayVM;
	
	abstract implementation OPCUADDSGatewayVM.impl
		subcomponents
			OPCUADDSGateway: abstract OPCUADDSGateway.impl;
			CustomerIIoTSoftware : abstract FORA::core::Application;
	end OPCUADDSGatewayVM.impl;

	abstract VirtualMachine
	end VirtualMachine;

	abstract implementation VirtualMachine.impl
		subcomponents
			Applications : abstract FORA::core::Application[]; -- TSDB server, cloud connector, Customer IIoT software, ...
	end VirtualMachine.impl;
	
	abstract RTVirtualMachine
	end RTVirtualMachine;
	
	abstract implementation RTVirtualMachine.impl
		subcomponents
			OPCUAClient: abstract FORA::core::Application;
			CustomerIIoTSoftware: abstract FORA::core::Application;
	end RTVirtualMachine.impl;

	data PayloadData
	end PayloadData;

	data OPCUAData extends PayloadData
	end OPCUAData;

	data DDSData extends PayloadData
	end DDSData;

	device PLCDevice
		features
			TSN_0: requires bus access FORA::hardware::RJ45;
	end PLCDevice;
	
end UC1;